#!/bin/csh -f
#
# runsome {plm | ppm} {2d | 3d} testname wave minres maxres
#
# Compute results for one wave number and a variety of resolutions
#

# Get the predictor step to use, plm or ppm, and the dimension, 2d or 3d
# and the test to use
set pred=${1}
set dim=${2}
set testname=${3}

# Get the wave # of the plane wave
set wave=(${4})

# Get the minimum and maximum resolutions for the computation
set minres=${5}
set maxres=${6}

# Start at the maximum resolution
set res=${maxres}

# Loop through all resolutions
while (${res} > ${minres})
  # Output the current resolution
  echo ${res} | awk '{printf("    %4d...",$1);}'

  # Run the computation for this resolution and the next coarser resolution
  runtwo ${pred} ${dim} ${testname} "${wave}" ${res}

  # Done with the computation
  echo ""

  # Move onto the next (lower) resolution
  set res=`expr ${res} / 2`
end
