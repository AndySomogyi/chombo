C      _______              __
C     / ___/ /  ___  __ _  / /  ___
C    / /__/ _ \/ _ \/  V \/ _ \/ _ \
C    \___/_//_/\___/_/_/_/_.__/\___/
C    Please refer to Copyright.txt, in Chombo's root directory.

#include "CONSTANTS.H"

c
c piecewise constant interpolation
c
      subroutine interpconstant(
     &     CHF_FRA[fine],
     &     CHF_CONST_FRA[coarse],
     &     CHF_BOX[b],
     &     CHF_CONST_INT[ref_ratio],
     &     CHF_BOX[bref]
     &     )

      integer var
      integer CHF_DDECL[ic; jc; kc]
      integer CHF_DDECL[if; jf; kf]
      integer CHF_DDECL[ii; jj; kk]

c compute on the refined box of each coarse point
      do var = 0, CHF_NCOMP[coarse] - 1
         CHF_MULTIDO[ b; ic; jc; kc ]
            CHF_MULTIDO[ bref; ii; jj; kk ]
            CHF_DTERM[
               if = ic*ref_ratio + ii;
               jf = jc*ref_ratio + jj;
               kf = kc*ref_ratio + kk]
               fine(CHF_IX[if;jf;kf],var) = coarse(CHF_IX[ic;jc;kc],var)
            CHF_ENDDO
         CHF_ENDDO
      end do

      return
      end

c
c calculate slopes with central difference formula
c
      subroutine interpcentralslope (
     &     CHF_FRA [slope],
     &     CHF_CONST_FRA[state],
     &     CHF_BOX[b],
     &     CHF_CONST_INT[dir]
     &     )

      integer CHF_DDECL[i,ii; j,jj; k,kk]
      integer var

      CHF_DTERM[
      ii = CHF_ID(0,dir);
      jj = CHF_ID(1,dir);
      kk = CHF_ID(2,dir)]

      do var = 0, CHF_NCOMP[state] - 1
         CHF_MULTIDO [ b; i; j; k]

          slope (CHF_IX[i;j;k],var) = half * (
     &        state (CHF_IX[i+ii; j+jj; k+kk],var) -
     &        state (CHF_IX[i-ii; j-jj; k-kk],var) )

          CHF_ENDDO
       end do

      return
      end
c
c calculate slopes with high one-sided difference formula
c
      subroutine interphisideslope (
     &     CHF_FRA [slope],
     &     CHF_CONST_FRA [state],
     &     CHF_BOX[b],
     &     CHF_CONST_INT [dir]
     &     )

      integer CHF_DDECL[i,ii; j,jj; k,kk]
      integer var

      CHF_DTERM[
      ii = CHF_ID(0,dir);
      jj = CHF_ID(1,dir);
      kk = CHF_ID(2,dir)]

      do var = 0, CHF_NCOMP[state] - 1
         CHF_MULTIDO [ b; i; j; k]

          slope (CHF_IX[i;j;k],var) =
     &          state ( CHF_IX[i+ii;j+jj;k+kk], var)
     &        - state ( CHF_IX[i   ;j   ;k   ], var)

          CHF_ENDDO
       enddo

      return
      end
c
c calculate slopes with low one-sided difference formula
c
      subroutine interplosideslope (
     &     CHF_FRA [slope],
     &     CHF_CONST_FRA [state],
     &     CHF_BOX[b],
     &     CHF_CONST_INT [dir]
     &     )

      integer CHF_DDECL[i,ii; j,jj; k,kk], var

      CHF_DTERM[
      ii = CHF_ID(0,dir);
      jj = CHF_ID(1,dir);
      kk = CHF_ID(2,dir)]

      do var = 0, CHF_NCOMP[state] - 1
         CHF_MULTIDO [ b; i; j; k]

         slope (CHF_IX[i;j;k],var) =
     &        state ( CHF_IX[ i   ;j   ;k   ], var) -
     &        state ( CHF_IX[ i-ii;j-jj;k-kk], var)
          CHF_ENDDO
       end do

      return
      end
c
c multidimensional limiting
c
c bn:  box which spans the neighbors of a cell (generally (-1,1) in
c      each coordinate direction
c bcoarse: coarse-level box over which to limit slopes
c

      subroutine interplimit (
     &     CHF_FRA [ islope ],
     &     CHF_FRA [ jslope ],
     &     CHF_FRA [ kslope ],
     &     CHF_CONST_FRA [ state ],
     &     CHF_BOX [ bcoarse ],
     &     CHF_BOX [ bn ],
     &     CHF_BOX [ physdomain]
     &     )

      integer CHF_DDECL [ i ; j ; k  ], var
      integer CHF_DDECL [ ii; jj; kk ]
      integer CHF_DDECL [ in; jn; kn ]
      REAL_T statemax, statemin, deltasum,  eta
c       REAL_T tempone, tempzero, minuscule

c       tempone = one
c       tempzero = zero
c       minuscule = 1.e-12

      do var = 0, CHF_NCOMP[ islope ] - 1
         CHF_MULTIDO [ bcoarse; i; j; k]
             statemax = state ( CHF_IX[i;j;k], var )
             statemin = state ( CHF_IX[i;j;k], var )

             CHF_MULTIDO [ bn; ii; jj; kk ]

             CHF_DTERM[
                 in = i + ii;
                 jn = j + jj;
                 kn = k + kk]

                 if (
                 CHF_DTERM[
     &                in .ge. CHF_LBOUND[ state; 0 ] .and.
     &                in .le. CHF_UBOUND[ state; 0 ] ;
     &                .and.
     &                jn .ge. CHF_LBOUND[state; 1 ] .and.
     &                jn .le. CHF_UBOUND[ state; 1 ] ;
     &                .and.
     &                kn .ge. CHF_LBOUND[ state; 2 ] .and.
     &                kn .le. CHF_UBOUND[ state; 2 ] ]
     &                ) then
                    statemax = max ( statemax, state(CHF_IX[in;jn;kn],var))
                    statemin = min ( statemin, state(CHF_IX[in;jn;kn],var))
                 endif
             CHF_ENDDO
             deltasum = half * (
                CHF_DTERM[
     &            abs ( islope ( CHF_IX[i;j;k], var ) );
     &            +
     &            abs ( jslope ( CHF_IX[i;j;k], var ) );
     &            +
     &            abs ( kslope ( CHF_IX[i;j;k], var ) )]
     &            )
c
c (TJL) the slope limiting is being done differently to avoid the problem(s)
c GHM mentions below.
c
c              if ( deltasum .gt. zero ) then
c c
c c (GHM) deltasum can be arbitrarily small, while the numerator
c c statemax-state can be arbitrarily large, and so an FP overflow
c c may occur (this happens when state is a local extrema).
c c since eta is limited to <=tempone anyway, rewrite to avoid FPE.
c c
c c               etamax = ( statemax - state ( CHF_IX[i;j;k], var ) )
c c    &               / deltasum
c c               etamin = ( state ( CHF_IX[i;j;k], var ) - statemin )
c c    &               / deltasum
c                 deltasum = max(deltasum,minuscule)
c                 etamax = min( statemax - state ( CHF_IX[i;j;k], var ),
c      &                   tempone*deltasum )
c                 etamax = etamax/deltasum
c                 etamin = min( state ( CHF_IX[i;j;k], var ) - statemin,
c      &                   tempone*deltasum )
c                 etamin = etamin/deltasum
c                 eta = max ( min ( etamin, etamax, tempone ), tempzero )

              eta = min(statemax - state(CHF_IX[i;j;k],var),
     &                  state(CHF_IX[i;j;k],var) - statemin)

c Believe it or not, the following data snuck past the (deltasum>eta)
c test, then did a divide by zero.  Apparently not all zeros are
c created equal.
c deltasum=  0.0000000000000000E+000 eta=  0.0000000000000000E+000
c This result was obtained in a test using double precision with
c pgf77.  This error did not occur with the same initial conditions
c using double precision, but g77.  Go figure.  To fix this problem,
c I recon that if eta and deltasum are both close to zero, we ought
c to limit fully ...
c GHM 9/5/02
              if( eta .le. 1.e-9*abs(statemax) ) then
                 eta = zero
              else

              if (deltasum .gt. eta) then
                eta = eta/deltasum
              else
                eta = one
              endif

c GHM 9/5/02
              endif

c
c (TJL) this needs to be revisited.
c
c c ::: design decision not to limit slopes near physical boundaries
c c               now enforced in FineInterp() calling function
c c               by modification of the box size bcoarse.
c c               GHM 7/12/01
c
c c               CHF_DTERM[
c c               if (i.eq.CHF_LBOUND[physdomain;0]) then
c c                  eta = one
c c               else if (i.eq.CHF_UBOUND[physdomain;0]) then
c c                  eta = one ;
c c               else if (j.eq.CHF_LBOUND[physdomain;1]) then
c c                  eta = one
c c               else if (j.eq.CHF_UBOUND[physdomain;1]) then
c c                  eta = one ;
c c               else if (k.eq.CHF_LBOUND[physdomain;2]) then
c c                  eta = one
c c               else if (k.eq.CHF_UBOUND[physdomain;2]) then
c c                  eta = one ]
c c               endif

              CHF_DTERM[
              islope ( CHF_IX[i;j;k], var ) =
     &             eta * islope ( CHF_IX[i;j;k], var ) ;
              jslope ( CHF_IX[i;j;k], var ) =
     &             eta * jslope ( CHF_IX[i;j;k], var ) ;
              kslope ( CHF_IX[i;j;k], var ) =
     &             eta * kslope ( CHF_IX[i;j;k], var )  ]
c              end if
         CHF_ENDDO
      end do

      return
      end

c
c piecewise bi-(tri-)linear interpolation to "fine" assuming that
c piecewise constant interpolation has already been done
c
      subroutine interplinear (
     &     CHF_FRA [ fine ],
     &     CHF_CONST_FRA [ slope ],
     &     CHF_BOX [ b ],
     &     CHF_CONST_INT [ dir ],
     &     CHF_CONST_INT [ ref_ratio ],
     &     CHF_BOX[bref]
     &     )

      integer CHF_DDECL[ ic; jc; kc ]
      integer CHF_DDECL[ if; jf; kf ]
      integer CHF_DDECL[ ii; jj; kk ]
      integer var, id

      REAL_T dxf
c this is a box that will go from (0,0,0) to (ref_ratio-1)*(1,1,1)

c compute on the refined box of each coarse point
      do var = 0, CHF_NCOMP[fine] - 1
          CHF_MULTIDO[ b; ic; jc; kc ]

              CHF_MULTIDO[ bref; ii; jj; kk ]

              CHF_DTERM[
                  if = ic*ref_ratio + ii;
                  jf = jc*ref_ratio + jj;
                  kf = kc*ref_ratio + kk ]

              CHF_DTERM[
                  if (dir .eq. 0) then
                      id = ii;
                  else if (dir .eq. 1) then
                      id = jj;
                  else if (dir .eq. 2) then
                      id = kk]
                  endif
                  dxf = -half + ( (id+half) / ref_ratio )
                  fine( CHF_IX[if;jf;kf],var) =
     &                 fine( CHF_IX[if;jf;kf],var) +
     &                 dxf * slope ( CHF_IX [ ic; jc; kc ], var )
              CHF_ENDDO
          CHF_ENDDO
      end do

      return
      end


!     -----------------------------------------------------------------
!     subroutine INTERPHOMO
!
!     INPUTS/OUTPUTS:
!     phi      <=> phi
!     region   <=  ???
!     x1       <=  ???
!     dxCrse   <=  ???
!     idir     <=  ???
!     ihilo    <=> ???
!
!     Author: ???
!     Note: moved from EllipticDesign/AMRPoissonOpF.ChF by <dbs> Jan2006
!     ------------------------------------------------------------------

      subroutine INTERPHOMO(
     &    CHF_FRA[phi],
     &    CHF_BOX[region],
     &    CHF_CONST_REAL[x1],
     &    CHF_CONST_REAL[dxCrse],
     &    CHF_CONST_INT[idir],
     &    CHF_CONST_INT[ihilo])

      REAL_T x2, denom, idenom, x, xsquared, m1, m2
      REAL_T q1, q2
      REAL_T pa, pb, a, b
      INTEGER ncomp,  n
      INTEGER CHF_DDECL[ii,i;jj,j;kk,k]

      x2 = half*(three*x1+dxCrse)
      denom = one-((x1+x2)/x1)
      idenom = one/(denom)
      x = two*x1
      xsquared = x*x

      m1 = one/(x1*x1)
      m2 = one/(x1*(x1-x2))

      q1 = one/(x1-x2)
      q2 = x1+x2
      ihilo = ihilo*(-1)

      ncomp = CHF_NCOMP[phi]
      CHF_DTERM[
      ii = CHF_ID(idir, 0)*ihilo;
      jj = CHF_ID(idir, 1)*ihilo;
      kk = CHF_ID(idir, 2)*ihilo]

      do n = 0, ncomp-1
          CHF_MULTIDO[region; i; j; k]
          pa=phi(CHF_IX[i+2*ii;j+2*jj;k+2*kk],n)
          pb=phi(CHF_IX[i+ii;j+jj;k+kk],n)
          a=((pb-pa)*m1 - (pb)*m2)*idenom
          b=(pb)*q1 - a*q2
          phi(CHF_IX[i;j;k],n) = a*xsquared + b*x + pa
          CHF_ENDDO
      enddo

      return

      end
