C      _______              __
C     / ___/ /  ___  __ _  / /  ___
C    / /__/ _ \/ _ \/  V \/ _ \/ _ \
C    \___/_//_/\___/_/_/_/_.__/\___/
C    Please refer to Copyright.txt, in Chombo's root directory.

#include "CONSTANTS.H"

c -----------------------------------------------------------------
c this subroutine averages the edge-centered edgeData to the cell-centered
c cellData in the direction dir
c
c INPUTS/OUTPUTS:
c edgeData  => edge-centered field
c cellData <=  cell-centered field
c cellBox   => box which defines cellData valid region
c dir       => direction in which to average
c -----------------------------------------------------------------
      subroutine EDGETOCELL(CHF_CONST_FRA1[edgeData],
     &                      CHF_FRA1[cellData],
     &                      CHF_BOX[cellBox],
     &                      CHF_CONST_INT[dir])

c ::: local variables
      integer CHF_DDECL[i;j;k]
      integer CHF_DDECL[ii;jj;kk]

      CHF_MULTIDO[cellBox;i;j;k]
      CHF_DTERM[
      ii = i+CHF_ID(0,dir);
      jj = j+CHF_ID(1,dir);
      kk = k+CHF_ID(2,dir)]

      cellData(CHF_IX[i;j;k]) = half*(
     &                    edgeData(CHF_IX[i;j;k])
     &                   +edgeData(CHF_IX[ii;jj;kk]))

      CHF_ENDDO

      return
      end
